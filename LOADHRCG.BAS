100  REM  PROGRAM TO LOAD HRCG
110  ONERR  GOTO 330
120 ADRS = 0
130  PRINT  CHR$(4);"BLOAD RBOOT"
140  CALL 520: REM  EXECUTE RBOOT
150 ADRS =  USR(0),"HRCG"
160  REM  BRING IN HRCG, ADRS=STARTING ADDRESS
170  PRINT "HOW MANY ALTERNATE CHARACTER SETS"
180  INPUT "WOULD YOU LIKE (0-9)? ";A
190  IF (A <0)  OR (A >9)  THEN 170
200  IF A = 0  THEN 300
210  IF ADRS < = 0  THEN ADRS = ADRS +65536: REM  MAKE ADRS POSITIVE
220 CS = ADRS -768 *A: HIMEM: CS
230 CH =  INT(CS/256):CL = CS -256 *CH
240  POKE ADRS +7,CL: POKE ADRS +8,CH
250  FOR I = 1 TO A
260  PRINT "NAME OF CHARACTER SET ";I;
270  INPUT CN$
280  PRINT  CHR$(4);"BLOAD ";CN$;",A";CS +(I -1) *768: REM  LOAD IN SET
290  NEXT I
300  CALL ADRS: REM  INITIALIZE HRCG
310  POKE 216,0: REM  ONERR OFF
320  END 
330  REM  ERROR RECOVERY ROUTINE
335  POKE 768,104: POKE 769,168: POKE 770,104: POKE 771,166: POKE 772,223: POKE 773,154: POKE 774,72: POKE 775,152: POKE 776,72: POKE 777,96
340  CALL 768: REM  CLEAN UP THE STACK
342 LN =  PEEK(218) + PEEK(219) *256
343  IF LN = 180  THEN  PRINT "PLEASE ENTER AN INTEGER 0-9": GOTO 170
345  PRINT "UNABLE TO LOAD": GOTO 260
350  END 